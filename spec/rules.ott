%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% METAVARIABLES

metavar typevar, X , Y ::= {{ repr-locally-nameless }}
  
indexvar I, J, h, i, j, n, m ::= {{ coq nat }}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
grammar
l, k :: 'lbl_' ::=
  | i ::  :: TagIndex
  | tleft :: :: TagLeft {{ tex \mathrm{left} }}
  | tright :: :: TagRight {{ tex \mathrm{right} }}
  

A , B , C :: 't_' ::= {{ com value type }}
  | X                                ::  :: tvar
  | In l A                           ::  :: rcd      {{ tex \tytag{[[l]]}{[[A]]} }}
  | A1 &  A2                         ::  :: and
  | A1 |  A2                         ::  :: or
  | A -> B                           ::  :: arrow
  | forall X . B                     ::  :: forall   (+ bind X in B +)
  | Top                              ::  :: top
  | Empty                            ::  :: bot

  | Tcov [ A ]                       :: M:: ctx      {{ icho (appctx_Tcov [[Tcov]] [[A]]) }}
  | ( A )                            :: S:: paren    {{ icho [[A]] }}
  | A [ X ~> B ]                     :: M:: tsubst   {{ coq (open_typ_wrt_typ [[X]][[A]][[B]]) }}


Tcov {{ tex \tau }} :: 'ty_ctx_cov' ::= {{ com covariant type context }}
  | In l [-]            :: :: TcovIn   {{ tex \tytag{[[l]]}{\hole} }}
  | A -> [-]            :: :: TcovArr
  | forall [-]          :: :: TcovAll
  | [-] & A             :: :: TCovInterL
  | A & [-]             :: :: TCovInterR
  | [-] | A             :: :: TCovUnionL
  | A | [-]             :: :: TCovUnionR

terminals :: 'terminals_' ::=
  | [-]          ::    :: hole      {{ tex \hole }}
  | &            ::    :: intersect {{ tex \sqcap }}
  | |            ::    :: union     {{ tex \sqcup }}
  | forall       ::    :: forall    {{ tex \forall }}
  | exists       ::    :: exists    {{ tex \exists }}
  | nil          ::    :: nil       {{ tex \kw{*} }}
  | notin        ::    :: notin     {{ tex \notin }}
  | Top          ::    :: topty     {{ tex \tytop }}
  | Empty        ::    :: emptyty   {{ tex \tyempty }}

formula :: formula_ ::=  
   | judgement                       ::   :: judgement
   | l != l'                         ::   :: labNeq
   | l = l'                          ::   :: labEq
   | ( formula )                     ::   :: formulaParen

%contextrules
%  Tcov _:: A :: A


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
embed
{{ coq
(** context application *)
Definition appctx_Tcov (Tcov5:Tcov) (A_6:A) : A :=
  match Tcov5 with
  | (ty_ctx_covTcovIn l5) => (t_rcd l5 A_6)
  | (ty_ctx_covTcovArr A5) => (t_arrow A5 A_6)
  | (ty_ctx_covTcovAll) => (t_forall A_6)
  | (ty_ctx_covTCovInterL A5) => (t_and A_6 A5)
  | (ty_ctx_covTCovInterR A5) => (t_and A5 A_6)
  | (ty_ctx_covTCovUnionL A5) => (t_or A_6 A5)
  | (ty_ctx_covTCovUnionR A5) => (t_or A5 A_6)
end.
}}

defns
DSub :: '' ::=

  defn
  A <: B
  :: :: DeclarativeSubtyping :: DSub_
  {{ com Declarative subtyping }}
  by


  ----------- :: Refl
  A <: A

  A1 <: A2
  A2 <: A3
  ------------- :: Trans
  A1 <: A3

  A <: B
  -------------------- :: CovIn %1
  In l A <: In l B
  
  A <: B
  -------------------- :: CovArr %2
  C -> A <: C -> B
  
  A <: B
  --------------------------- :: CovAll %3
  forall X. A <: forallX. B
  
  A <: B
  -------------------- :: CovInterL %4
  A & C <: B & C
  
  A <: B
  -------------------- :: CovInterR %5
  C & A <: C & B

  A <: B
  -------------------- :: CovUnionL %6
  A | C <: B | C
  
  A <: B
  -------------------- :: CovUnionR %7
  C | A <: C | B
  
  A2 <: A1
  -------------------- :: FunCon
  A1 -> B <: A2 -> B

  ------------------------------------- :: CovDistIn %1
  (In l A) & (In l B) <: In l (A & B)
  
  ------------------------------------- :: CovDistArr %2
  (C -> A) & (C -> B) <: (C -> A & B)
  
  --------------------------------------------------- :: CovDistAll %3
  (Forall X. A) & (Forall X. B) <: (Forall X. A & B)
  
  ------------------------------------- :: CovDistInterL %4
  (A & C) & (B & C) <: (A & B) & C
  
  ------------------------------------- :: CovDistInterR %5
  (C & A) & (C & B) <: C & (A & B)
  
  ------------------------------------- :: CovDistUnionL %6
  (A | C) & (B | C) <: (A & B) | C
  
  ------------------------------------- :: CovDistUnionR %7
  (C | A) & (C | B) <: C | (A & B)

  ------------------------------------------------- :: FunDistI
  (A1 -> B) & (A2 -> B) <: (A1 | A2) -> B 

% I think these are derivable from the above

  % --------------------------------------- :: UnionDistI 
  % A1 & (A2 | A3) <: (A1 & A2) | (A1 & A3)

  % --------------------------------------- :: InterDistU
  % A1 | (A2 & A3) <: (A1 | A2) & (A1 | A3)

  A1 <: B
  ----------------- :: InterLL
  A1 & A2 <: B

  A2 <: B
  ----------------- :: InterLR
  A1 & A2 <: B

  A <: B1
  A <: B2
  ----------------- :: InterR
  A <: B1 & B2

  A1 <: B
  A2 <: B
  ------------ :: UnionL
  A1 | A2 <: B

  A <: B1
  ------------ :: UnionRL
  A <: B1 | B2

  A <: B2
  ------------ :: UnionRR
  A <: B1 | B2
  
  ---------- :: Empty
  Empty <: B

  --------- :: Top
  A <: Top


defns
Ordinary :: '' ::=

  defn ordinaryU A :: :: UnionOrdinary :: OrdU_
  by

  ------------- :: top
  ordinaryU Top

  --------------- :: bot
  ordinaryU Empty

  ---------------- :: arrow
  ordinaryU A -> B

  ordinaryU A
  ordinaryU B
  --------------- :: and
  ordinaryU A & B

  ordinaryU A
  ---------------- :: rcd
  ordinaryU In l A

  ordinaryU A
  ---------------------- :: forall
  ordinaryU forall X . A


  defn ordinaryI A :: :: IntersectionOrdinary :: OrdI_
  by

  ------------- :: top
  ordinaryI Top

  --------------- :: bot
  ordinaryI Empty

  ordinaryU A
  ordinaryI B
  ---------------- :: arrow
  ordinaryI A -> B

  ordinaryI A
  ordinaryI B
  --------------- :: or
  ordinaryI A | B

  ordinaryI A
  ---------------- :: rcd
  ordinaryI In l A

  ordinaryI A
  ---------------------- :: forall
  ordinaryI forall X . A


defns
Split :: '' ::=

  defn splitI A = B1 & B2 :: :: SplitIntersection :: SpI_
  by

  -------------------- :: Intersection
  splitI A & B = A & B

  splitI A = A1 & A2
  ---------------------------------- :: UnionL
  splitI A | B = A1 | B & A2 | B

  splitI B = B1 & B2
  ---------------------------------- :: UnionR
  splitI A | B = A | B1 & A | B2

  splitI B = B1 & B2
  --------------------------------- :: FunR
  splitI A -> B = A -> B1 & A -> B2

  splitU A = A1 | A2
  --------------------------------- :: FunL
  splitI A -> B = A1 -> B & A2 -> B

  splitI A = A1 & A2
  --------------------------------------------------- :: FunTy
  splitI forall X . A = forall X . A1 & forall X . A2

  splitI A = A1 & A2 
  --------------------------------- :: In
  splitI In l A = In l A1 & In l A2


  defn splitU A = B1 | B2 :: :: SplitUnion :: ''
  by

  -------------------- :: SpUUnion
  splitU A | B = A | B

  splitU A = A1 | A2
  ------------------------------ :: SpUIntersectionL
  splitU A & B = A1 & B | A2 & B

  splitU B = B1 | B2
  ------------------------------ :: SpUIntersectionR
  splitU A & B = A & B1 | A & B2

  splitU B = B1 | B2
  --------------------------------- :: SpUFunR
  splitU A -> B = A -> B1 | A -> B2

  splitI A = A1 & A2
  --------------------------------- :: SpUFunL
  splitU A -> B = A1 -> B | A2 -> B

  splitU A = A1 | A2
  --------------------------------------------------- :: SpUFunTy
  splitU forall X . A = forall X . A1 | forall X . A2

  splitU A = A1 | A2 
  --------------------------------- :: SpUIn
  splitU In l A = In l A1 | In l A2
