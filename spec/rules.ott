%%Challenge: A & ( B | C ) <: ( A & B ) | ( A & C )
%%Workaround: ignore it.
%%* omit record types

%%--------------------------------------------------------------

%%Type

%%OrdinaryType   A, B, C, D ::= Int | Top | Bot | A -> B | A & B | A \/ B

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% GRAMMAR (LNgen portion)

grammar

typ, A, B, C, D, char {{ tex \mathsf{Char} }}, bool {{ tex \mathsf{Bool} }} % , ll {{ tex \{ l \} }}
  :: 't_'             ::=                  {{ com types }}
  | int               ::   :: int          {{ com int }} {{ tex \mathsf{Int} }}
  | Top               ::   :: top          {{ com top type }} {{ tex \mathsf{Top} }}
  | Bot               ::   :: bot          {{ com bottom type }} {{ tex \mathsf{Bot} }}
  | A -> B            ::   :: arrow        {{ com function types }}
  | A & B             ::   :: and          {{ com intersection }}
  | A \/ B            ::   :: or           {{ com union }}
  | ( A )             :: S :: paren        {{ coq [[A]] }}
%  | { l : A }         ::   :: rcd          {{ com record }}
  | | mode |          :: S :: topbot       {{ coq (typbymode[[mode]]) }}
  | [ A mode B ]      :: S :: choose       {{ coq (choose[[mode]][[A]][[B]]) }}


mode {{ tex \Diamond }}, m {{ tex \Diamond }}
  :: 'm_'             ::=
  | sub               ::   :: sub          {{ tex < }}
  | super             ::   :: super        {{ tex > }}
  | ! mode            :: S :: flip         {{ tex \overline{[[mode]]} }} {{ coq (flipmode [[mode]]) }}

parsing
  t_or <= t_and
  t_arrow <= t_or
  t_arrow <= t_and
  t_arrow right t_arrow
  
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% GRAMMAR  (non LNgen portion)

grammar


terminals :: 'terminals_' ::=
  | ->               ::  :: arrow      {{ tex \rightarrow }}
  | &                ::  :: and        {{ tex \, \& \, }}
  | \/               ::  :: or         {{ tex \, \mid \, }}
  | ,                ::  :: break      {{ tex , }}
  | <:b              ::  :: subb       {{ tex <: }}
  | <::              ::  :: subd       {{ tex <: }}
  | ||-              ::  :: turnstileb {{ tex \vdash }}
  | |-&              ::  :: wfCombined {{ tex \vdash_{\&} }}
  | <|               ::  :: triangleL  {{ tex \mathbin{\lhd} }}
  | |>               ::  :: triangleR  {{ tex \mathbin{\rhd} }}


formula :: 'formula_' ::=
  | judgement        ::   :: judgement
  | not formula      :: M :: not
    {{ tex \neg [[formula]] }}
    {{ coq not ([[formula]]) }}
  | ( formula )      :: M :: brackets
    {{ tex ([[formula]]\!) }}
    {{ coq [[formula]] }}
  | < formula >              :: M :: high_form
    {{ tex \hlmath{[[formula]]} }}
    {{ coq [[formula]] }}
  | l1 <> l2                 ::   :: label
    {{ coq [[l1]] <> [[l2]] }}
    {{ tex [[l1]] \neq [[l2]] }}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

embed
{{ coq

Definition flipmode (m: mode) :=
  match m with
  | m_sub => m_super
  | m_super => m_sub
  end.
  
Definition typbymode (m: mode) :=
  match m with
  | m_sub => t_top
  | m_super => t_bot
  end.
  
Definition choose (m: mode) (A: typ) (B: typ) :=
  match m with
  | m_sub => t_and A B
  | m_super => t_or A B
  end.

}}

defns
OSubtyping :: '' ::=

defn
A < mode > B  :: :: osub :: OS_
{{ com Original BCD Declarative Subtyping - Top-Like + Unions }}
{{ tex [[A]] [[mode]] [[B]] }}
by


---------------------- :: refl
A < mode > A


A < mode >  B
B < mode > C
--------------- :: trans
A < mode > C


------------------ :: top
A < mode > |mode|


A < ! mode > B
C < mode >  D
------------------------ :: arr
A -> C < mode > B -> D


A < mode > B
A < mode > C
---------------------- :: and
A < mode > [B mode C]


------------------------- :: andl
[A mode B] < mode > A


------------------------- :: andr
[A mode B] < mode > B


--------------------------------------- :: distArr
(A -> B) & (A -> C) < mode > A -> B & C


--------------------------------------- :: distArrU
(A -> C) & (B -> C) < mode > A \/ B -> C


%-------------------------------------- :: distAnd
%(B1 \/ B2) & A < (B1 & A) \/ (B2 & A)


-------------------------------------------------------------------- :: distOr
[[A1 !mode B] mode [A2 !mode B]] < mode > [[A1 mode A2] !mode B]


B  < ! mode > A
------------------------- :: flip
A  < mode > B


defns
OrdinaryType :: '' ::=

defn
ordinary m A :: :: ord :: O_
{{ com Ordinary Types }}
{{ tex [[A]]~\mathsf{Ordinary}_{[[m]]} }}
by


---------------- :: top
ordinary m Top


---------------- :: bot
ordinary m Bot


---------------- :: int
ordinary m int


--------------------- :: arrowUnion
ordinary super A->B


ordinary super  A
ordinary sub B
--------------------- :: arrow
ordinary sub A->B


ordinary m A
ordinary m B
--------------------- :: or
ordinary m [A !m B]



defns
SplitType :: '' ::=

defn
split m A B C :: :: spl :: Sp_
{{ com Splittable Types }}
{{ tex [[B]] \mathbin{\lhd}_{[[m]]} [[A]] \mathbin{\rhd}_{[[m]]} [[C]] }}
% {{ tex [[B]] \mathbin{\LHD} [[A]] \mathbin{\RHD} [[C]] }}
by


-----------------------:: and
split m [A m B] A B


split sub B C D
---------------------------- :: arrow
split sub A->B A->C A->D


ordinary sub D
split super A B C
-------------------------- :: arrowUnion
split sub A->D B->D C->D


split m A A1 A2
--------------------------------------- :: orl
split m [A !m B] [A1 !m B] [A2 !m B]


ordinary m A
split m B B1 B2
--------------------------------------- :: orr
split m [A !m B] [A !m B1] [A !m B2]



defns
Subtyping :: '' ::=

defn
A <: mode :> B  :: :: sub :: S_
{{ com Algorithmic Subtyping }}
{{ tex [[A]] :[[mode]]: [[B]] }}
by


---------------------- :: int
int <: mode :> int


---------------------- :: top
A <: mode :> |mode|


---------------------- :: bot
|!mode| <: mode :> A


%ordinary !mode D
%ordinary mode C
split mode D A B
A <: mode :> C
------------------------- :: andl
D <: mode :> C


%ordinary !mode D
%ordinary mode C
split mode D A B
B <: mode :> C
----------------- :: andr
D <: mode :> C


%ordinary !mode D
%ordinary mode C
split !mode C A B
D <: mode :> A
------------------------- :: orl
D <: mode :> C


%ordinary !mode D
%ordinary mode C
split !mode C A B
D <: mode :> B
----------------- :: orr
D <: mode :> C

ordinary sub A -> C
ordinary sub B -> D
A <: !mode :> B
C <: mode :> D
------------------------ :: arr
A -> C <: mode :> B -> D


% ordinary mode D
split !mode A B C
B <: mode :> D
C <: mode :> D
--------------- :: or
A <: mode :> D


split mode D B C
A <: mode :> B
A <: mode :> C
--------------- :: and
A <: mode :> D


%%%%%%%%%%%%%%%%%%%%%%%%%
% Duotyping rules above %
%
%
% Subtyping rules below %
%%%%%%%%%%%%%%%%%%%%%%%%%


defns
DSub :: '' ::=

defn
A < B  :: :: declarative_subtyping :: DS_
{{ com Declarative Subtyping }}
{{ tex [[A]] \leq [[B]] }}
by


---------------------- :: refl
A < A


A < B
B < C
--------------- :: trans
A < C


--------------- :: top
A < Top


--------------- :: bot
Bot < A


B < A
C < D
------------------------ :: arrow
A -> C < B -> D


A < B
A < C
--------------- :: and
A < B & C


------------------------- :: andl
A & B < A


----------------- :: andr
A & B < B


A < C
B < C
--------------- :: or
A \/ B < C


------------------------- :: orl
A < A \/ B


----------------- :: orr
B < A \/ B


-------------------------------------- :: distArr
(A -> B1) & (A -> B2) < A -> B1 & B2


-------------------------------------- :: distArrRev
 A -> B1 & B2 < (A -> B1) & (A -> B2)


-------------------------------------- :: distArrU
(A1 -> B) & (A2 -> B) < A1 \/ A2 -> B


-------------------------------------- :: distArrURev
A1 \/ A2 -> B < (A1 -> B) & (A2 -> B)


-------------------------------------- :: distOr
(A1 \/ B) & (A2 \/ B) < (A1 & A2) \/ B


-------------------------------------- :: distAnd
(A1 \/ A2) & B < (A1 & B) \/ (A2 & B)



%%%%%%%%%%%%%%%%%%%%%%
%Single Mode
%Algorithmic Subtyping
%%%%%%%%%%%%%%%%%%%%%%

defns
Ordinary :: '' ::=

defn
ordinary A :: :: single_ord :: SO_
{{ com Ordinary Types }}
{{ tex [[A]]~\mathsf{Ordinary} }}
by


---------------- :: top
ordinary Top


---------------- :: bot
ordinary Bot


---------------- :: int
ordinary int


ordinaryU A
ordinary B
---------------- :: arrow
ordinary A->B


ordinary A
ordinary B
---------------- :: or
ordinary A\/B


%ordinary B
%---------------- :: rcd
%ordinary {l:B}


defn
ordinaryU A :: :: ordu :: OU_
{{ com Ordinary Types (for unions) }}
{{ tex [[A]]~\mathsf{OrdinaryU} }}
by


---------------- :: top
ordinaryU Top


---------------- :: bot
ordinaryU Bot


---------------- :: int
ordinaryU int


---------------- :: arrow
ordinaryU A->B


% (A1\/A2)&(B1\/B2) ~= (A1&(B1\/B2)) \/ (A2&(B1\/B2))

ordinaryU A
ordinaryU B
---------------- :: and
ordinaryU A&B


%ordinaryU B
%---------------- :: rcd
%ordinaryU {l:B}



defns
Split :: '' ::=

defn
split A B C :: :: single_spl :: SSp_
{{ com Splittable Types }}
{{ tex [[B]] \mathbin{\lhd} [[A]] \mathbin{\rhd} [[C]] }}
by


----------------:: and
split A&B A B


split B C D
-------------------------- :: arrow
split A->B A->C A->D


%split B C D
%-------------------------- :: rcd
%split {l:B} {l:C} {l:D}


ordinary D
splitU A B C
-------------------------- :: arrowUnion
split A->D B->D C->D


split A A1 A2
-------------------------- :: orl
split A\/B A1\/B A2\/B


ordinary A
split B B1 B2
-------------------------- :: orr
split A\/B A\/B1 A\/B2


defn
splitU A B C :: :: splu :: SpU_
{{ com Split Union Types }}
{{ tex [[B]] \mathbin{\LHD} [[A]] \mathbin{\RHD} [[C]] }}
by


--------------------:: or
splitU A\/B A B


%splitU B C D
%--------------------------- :: arrow
%splitU A->B A->C A->D


%splitU B C D
%-------------------------- :: rcd
%splitU {l:B} {l:C} {l:D}


% (A1\/A2)&(B1\/B2) ~= (A1&(B1\/B2)) \/ (A2&(B1\/B2))

splitU A A1 A2
-------------------------- :: andl
splitU A&B A1&B A2&B


ordinaryU A
splitU B B1 B2
-------------------------- :: andr
splitU A&B A&B1 A&B2



defns
SSub :: '' ::=

defn
A <: B  :: :: singlemode_sub :: SS_
{{ com Algorithmic Subtyping }}
by


---------------------- :: int
int <: int


---------------------- :: top
A <: Top


---------------------- :: bot
Bot <:  A


split D A B
A <: C
------------------------- :: andl
D <: C


split D A B
B <: C
----------------- :: andr
D <: C


splitU C A B
D <: A
------------------------- :: orl
D <: C


splitU C A B
D <: B
----------------- :: orr
D <: C


ordinary A -> C
ordinary B -> D
B <: A
C <: D
------------------------ :: arrow
A -> C <: B -> D


split D B C
A <: B
A <: C
--------------- :: and
A <: D


splitU A B C
B <: D
C <: D
--------------- :: or
A <: D
